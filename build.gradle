/*
 * Copyright (c) 2014-2017 Afero, Inc. All rights reserved.
 */



buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.android.library' version '8.13.0' apply false
}

group = "io.afero.sdk"
version = "1.6.0"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

java {
    withJavadocJar()
}

apply plugin: 'java'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // https://github.com/FasterXML/jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'

    // https://github.com/ReactiveX/RxJava
    implementation 'io.reactivex:rxjava:1.3.8'

    testImplementation 'junit:junit:4.13.2'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

// Copy resources into build/classes so unit tests can find them. Must be a better way?
task copyResourcesDirectoryToClasses(type: Copy){
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test/resources"
}

testClasses.dependsOn copyResourcesDirectoryToClasses

publishing {
    repositories {
        maven {
	    url = 'https://afero.jfrog.io/afero/afero-java-sdk'
	    credentials {
	        username = project.findProperty('aferoArtifactoryUserName') ?: ""
		password = project.findProperty('aferoArtifactoryPassword') ?: ""
            }
	}
    }
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            version = rootProject.version
            artifactId project.name
            from components.java
        }
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.links("http://reactivex.io/RxJava/1.x/javadoc/")
    options.links("http://fasterxml.github.io/jackson-annotations/javadoc/2.8/")
    options.links("http://fasterxml.github.io/jackson-core/javadoc/2.8/")
    options.links("http://fasterxml.github.io/jackson-databind/javadoc/2.8/")
}
