/*
 * Copyright (c) 2014-2017 Afero, Inc. All rights reserved.
 */

plugins {
    id 'com.android.library'
    id 'maven-publish'
}

repositories {
    maven {
        url "https://afero.jfrog.io/afero/hubby-android"
        credentials {
            username project.aferoArtifactoryUserName
            password project.aferoArtifactoryPassword
        }
        authentication {
            basic(BasicAuthentication)
        }
        metadataSources {
            artifact()
        }
    }
}

final String hubbyVersion = "1.0.978"
final String hubType = project.findProperty('aferoSofthubType') ?: '"CONSUMER"'

android {
    namespace "io.afero.sdk.softhub"
    compileSdkVersion 35

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName rootProject.version // Correct: Inherits version from root.

        buildConfigField 'String', 'HUBBY_VERSION', '"' + hubbyVersion + '"'
        buildConfigField 'String', 'HUB_TYPE', hubType
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // It automatically creates a publication using the 'release' variant's AAR.
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':afero-sdk-core')
    api "io.afero.sdk:hubby:${hubbyVersion}@aar"
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'androidx.annotation:annotation:1.9.1'
    testImplementation 'org.robolectric:robolectric:4.16'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.retrofit2:retrofit:3.0.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'io.afero.sdk'
                artifactId = 'afero-sdk-softhub'
                version = rootProject.version
            }
        }
        repositories {
            maven {
                url = 'https://afero.jfrog.io/afero/afero-java-sdk'

                credentials {
                    username = project.aferoArtifactoryUserName
                    password = project.aferoArtifactoryPassword
                }
            }
        }
    }
}
