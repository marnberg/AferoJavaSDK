/*
 * Copyright (c) 2014-2017 Afero, Inc. All rights reserved.
 */


plugins {
    id 'com.android.application'
}

final String sdkRepoKey = project.findProperty('aferoSDKConsumeRepoKey') ?: 'afero-java-sdk'
final String sdkVersion = project.findProperty('aferoSDKVersion') ?: '1.6.0'

repositories {
    google()
    mavenCentral()
    maven {
        url "https://afero.jfrog.io/afero/${sdkRepoKey}"
        artifactUrls "https://afero.jfrog.io/artifactory/hubby-android"
        credentials {
            username = project.aferoArtifactoryUserName
            password = project.aferoArtifactoryPassword
        }
        authentication {
            basic(BasicAuthentication)
        }
        metadataSources {
            artifact()
        }
    }
}
configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-v4:27.1.0'
    }
}


android {
    namespace 'io.afero.aferolab'
    compileSdkVersion 35

    defaultConfig {
        applicationId 'io.afero.aferolab'
        minSdkVersion 26
        targetSdkVersion 35

        versionCode 1
        versionName '1.0'

        def getQuotedProp = { propName, defaultValue = null ->
            def value = project.findProperty(propName)
            if (value != null) {
                return "\"${value}\""
            }
            if (defaultValue != null) {
                return "\"${defaultValue}\""
            }
            return 'null'
        }

        buildConfigField 'String', 'AFERO_CLIENT_ID', getQuotedProp('aferoClientId')
        buildConfigField 'String', 'AFERO_CLIENT_SECRET', getQuotedProp('aferoClientSecret')
        buildConfigField 'String', 'AFERO_SERVICE_HOSTNAME', getQuotedProp('aferoServiceHostname', 'api.afero.io')
        buildConfigField 'String', 'AFERO_SOFTHUB_SERVICE', getQuotedProp('aferoSofthubService')
        buildConfigField 'String', 'AFERO_SOFTHUB_AUTHENTICATOR_CERT', getQuotedProp('aferoSofthubAuthenticatorCert')

        buildConfigField 'String', 'AFERO_OAUTH_AUTH_URL', getQuotedProp('aferoOauthAuthUrl')
        buildConfigField 'String', 'AFERO_OAUTH_TOKEN_URL', getQuotedProp('aferoOauthTokenUrl')
        buildConfigField 'String', 'AFERO_OAUTH_REDIRECT_SCHEME', getQuotedProp('aferoOauthRedirectScheme')
        buildConfigField 'String', 'AFERO_OAUTH_REDIRECT_HOST',  getQuotedProp('aferoOauthRedirectHost')

        def aferoOauthRedirectScheme = project.findProperty('aferoOauthRedirectScheme') ?: ""
        def aferoOauthRedirectHost = project.findProperty('aferoOauthRedirectHost') ?: ""
	println 'XXXaferoOauthRedirectScheme "' + aferoOauthRedirectScheme + '"'
	println 'XXXaferoOauthRedirectHost "' + aferoOauthRedirectHost + '"'
        manifestPlaceholders = [
                oAuthRedirectScheme: aferoOauthRedirectScheme,
                oAuthRedirectHost  : aferoOauthRedirectHost
        ]

    }

    buildTypes {
        debug {
            buildConfigField 'okhttp3.logging.HttpLoggingInterceptor.Level', 'HTTP_LOG_LEVEL', 'okhttp3.logging.HttpLoggingInterceptor.Level.BODY'
        }

        release {
            buildConfigField 'okhttp3.logging.HttpLoggingInterceptor.Level', 'HTTP_LOG_LEVEL', 'okhttp3.logging.HttpLoggingInterceptor.Level.BASIC'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false
        }
    }

    packagingOptions {
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        buildConfig true
        viewBinding true
    }
}

def camerax_version = "1.5.1"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // https://github.com/aferodeveloper/AferoJavaSDK
    implementation "io.afero.sdk:afero-sdk-core:${sdkVersion}"
    implementation "io.afero.sdk:afero-sdk-client-retrofit2:${sdkVersion}"
    implementation "io.afero.sdk:afero-sdk-android:${sdkVersion}@aar"
    implementation "io.afero.sdk:afero-sdk-softhub:${sdkVersion}@aar"

    implementation "io.afero.sdk:hubby:1.0.978@aar"

    // https://github.com/square/retrofit
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-jackson:3.0.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:3.0.0'

    // https://github.com/square/okhttp
    implementation 'com.squareup.okhttp3:okhttp:5.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.3.0'

    // https://github.com/ReactiveX/RxAndroid
    implementation 'io.reactivex:rxandroid:1.2.1'

    // https://github.com/ReactiveX/RxJava
    implementation 'io.reactivex:rxjava:1.3.8'

    // https://developers.google.com/ml-kit/vision/barcode-scanning
    implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.1'

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"


    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.13.0'

    //https://github.com/openid/AppAuth-Android
    implementation 'net.openid:appauth:0.11.1'
}
